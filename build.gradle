import io.izzel.arclight.gradle.tasks.UploadFilesTask

allprojects {
    group 'io.izzel.arclight'
    version '1.0.6-SNAPSHOT'

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    ext {
        agpVersion = '1.23'
        minecraftVersion = '1.19.2'
        forgeVersion = '43.3.8'
        apiVersion = '1.5.0'
        toolsVersion = '1.3.0'
        mixinVersion = '0.8.5'
        versionName = 'horn'
        gitHash = getGitHash()
    }

    task cleanBuild {
        doFirst {
            project.file("build/libs").deleteDir()
        }
    }
}

task collect(type: Copy) {
    destinationDir = file('build/libs')
    from { project(':arclight-forge').tasks.jar.outputs }
    dependsOn { project(':arclight-forge').tasks.jar }
}

def gitBranch() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

tasks.register('uploadFiles', UploadFilesTask) {
    mcVersion.set project.ext.minecraftVersion
    version.set "${project.version}-${project.ext.gitHash}"
    snapshot.set project.version.toString().endsWith("-SNAPSHOT")
    gitHash.set project.ext.gitHash
    branch.set gitBranch()
    inputs.files tasks.collect.outputs.files
    dependsOn(tasks.collect)
}
